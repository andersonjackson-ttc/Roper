Option Explicit

' get initials and notes from All Therapists sheet and put them in the correct
' boxes in the All Schedules sheet

Public Sub getInitialsAndNotes()
    ' declare variables
    Dim cell As range
    Dim newCell As range
    
    ' set first initial box cell
    Set newCell = Sheets("All Schedules").range("$O$1")
    
    For Each cell In Sheets("All Therapists").range("AllTherapistsInitials")
        ' if cell has initials and at least 1 room number, get initials
        If cell.value <> "-" And Not IsEmpty(cell) And Not IsEmpty(cell.Offset(0, 4)) Then
            newCell.value = cell.value
            If Not IsEmpty(cell.Offset(0, 25)) Then
                newCell.Offset(26, -14).value = cell.Offset(0, 25).value
            End If
            ' go to next initial box
            Set newCell = newCell.Offset(33, 0)
        End If
    Next cell
    
End Sub

' match initials from therapist schedule to initials in All Therapists sheet and grab
' room numbers for that therapist

Public Sub getRooms()
    Dim cell As range
    Dim newCell As range
    Dim rng As range
    Dim j As Integer
    Dim k As Integer
       
    ' set newCell to initial value - first initials box
    Set newCell = Sheets("All Schedules").range("$O$1")
    ' set counter for newCell row offset
    j = 3
    
    ' for each cell in All Therapists initials column, if value of cell (rng)
    ' matches value of newCell, grab the room numbers and put them in the correct
    ' schedule
    For Each rng In Sheets("All Therapists").range("AllTherapistsInitials")
        If newCell.value = rng.value Then
           ' set counter (k) for All Therapists initials cell column numbers
           For k = 4 To 21
            If Not IsEmpty(rng.Offset(0, k).value) Then
                newCell.Offset(j, -14).value = rng.Offset(0, k).value
                ' increment newCell row offset
                j = j + 1
            End If
           Next k
          ' go to the next therapist initials box in schedule
          Set newCell = newCell.Offset(33, 0)
        End If
       ' reset counter for newCell row offset
       j = 3
 
    Next rng
   
End Sub

' read rooms in All Schedules and match to 3W and 8P schedules; populate All Schedules
' for each room
Public Sub getSchedules()
    ' declare variables
    Dim therapistRoom As range
    Dim room3W As range
    Dim room8P As range
    Dim allSchedRooms As range
    Dim rooms3WSched As range
    Dim rooms8PSched As range
    
    ' set variables
    Set allSchedRooms = Sheets("All Schedules").range("RoomsAllSchedules")
    Set rooms3WSched = Sheets("3W Schedule").range("Rooms3WSchedule")
    Set rooms8PSched = Sheets("8P Schedule").range("Rooms8PSchedule")
    
    ' Grab rooms from 3W Schedule
    For Each therapistRoom In allSchedRooms
        For Each room3W In rooms3WSched
            If IsEmpty(therapistRoom.value) Or therapistRoom.value > 89 Then
                Exit For
            End If
            
            If therapistRoom.value = room3W.value Then
                range(room3W.Offset(0, 1), room3W.Offset(0, 22)).Copy
                range(therapistRoom.Offset(0, 4), therapistRoom.Offset(0, 24)).PasteSpecial xlPasteValues
                Exit For
            End If
        Next room3W
    Next therapistRoom
    
    ' Grab rooms from 8P Schedule
    For Each therapistRoom In allSchedRooms
        For Each room8P In rooms8PSched
            If therapistRoom.value = room8P.value Then
                range(room8P.Offset(0, 1), room8P.Offset(0, 22)).Copy
                range(therapistRoom.Offset(0, 4), therapistRoom.Offset(0, 24)).PasteSpecial xlPasteValues
                Exit For
            End If
        Next room8P
    Next therapistRoom
    
    ' turn cut copy mode off
    Application.CutCopyMode = False
End Sub



' Count number of 3W, 8P & 3P therapists and print the totals in a table
Public Sub countTherapists()
    Dim count3W As Integer
    Dim count8P As Integer
    Dim count3P As Integer
    Dim cell As range
    
    ' initialize counters
     count3W = 0
     count8P = 0
     count3P = 0
     
     For Each cell In Sheets("All Therapists").range("AllTherapistsInitials")
        If Not IsEmpty(cell) And cell.value <> "-" And Not IsEmpty(cell.Offset(0, 4)) Then
            If cell.Offset(0, 1).value = "3W" Then
                count3W = count3W + 1
            ElseIf cell.Offset(0, 1).value = "8P" Then
                count8P = count8P + 1
            Else
                count3P = count3P + 1
            End If
        End If
     Next cell

    ' add therapist count to sheet
    Sheets("All Schedules").range("$AD$8").value = count3W
    Sheets("All Schedules").range("$AD$9").value = count8P
    Sheets("All Schedules").range("$AD$10").value = count3P
    Sheets("All Schedules").range("$AD$11").value = count3W + count8P + count3P
    
End Sub
' apply conditional formatting to All Schedules
Public Sub allSchedCondFormat()
    ' declare variables
    Dim evalInt As range
    Dim roomCell As range
    Dim newEI As range
    Dim newRoom As range
    Dim evalInt8P As range
    Dim roomCell8P As range
    Dim newEI8P As range
    Dim newRoom8P As range
    Dim initialsBox As range
    Dim initialsCell As range
    Dim schedCell As range
    Dim initialsValue As String
     
    ' show that EVALs are highlighted in yellow
    With Sheets("All Schedules").range("EvalKeyBox")
        .value = "EVAL"
        .Font.Size = 9
        .HorizontalAlignment = xlCenter
        .VerticalAlignment = xlCenter
        .Interior.ColorIndex = 36
    End With
     ' show that INTs are highlighted in pink
    With Sheets("All Schedules").range("IntKeyBox")
        .value = "INT"
        .Font.Size = 9
        .HorizontalAlignment = xlCenter
        .VerticalAlignment = xlCenter
        .Interior.ColorIndex = 38
    End With
    
    ' highlight rooms in 3W with EVALs in yellow
    For Each evalInt In Sheets("3W Schedule").range("Eval_Int_3W")
        If LCase(evalInt.value) = "eval" Then
            For Each roomCell In Sheets("All Schedules").range("RoomsAllSchedules")
               If evalInt.Offset(0, 1).value = roomCell.value Then
                roomCell.Interior.ColorIndex = 36
               End If
            Next roomCell
        End If
    Next evalInt
        
      ' highlight rooms in 3W with INTs in pink
    For Each newEI In Sheets("3W Schedule").range("Eval_Int_3W")
        If LCase(newEI.value) = "int" Then
            For Each newRoom In Sheets("All Schedules").range("RoomsAllSchedules")
               If newEI.Offset(0, 1).value = newRoom.value Then
                newRoom.Interior.ColorIndex = 38
               End If
            Next newRoom
        End If
    Next newEI
    
    ' highlight rooms in 8P with EVALs in yellow
    For Each evalInt8P In Sheets("8P Schedule").range("Eval_Int_8P")
        If LCase(evalInt8P.value) = "eval" Then
            For Each roomCell8P In Sheets("All Schedules").range("RoomsAllSchedules")
               If evalInt8P.Offset(0, 1).value = roomCell8P.value Then
                roomCell8P.Interior.ColorIndex = 36
               End If
            Next roomCell8P
        End If
    Next evalInt8P
        
      ' highlight rooms in 8P with INTs in pink
    For Each newEI8P In Sheets("8P Schedule").range("Eval_Int_8P")
        If LCase(newEI8P.value) = "int" Then
            For Each newRoom8P In Sheets("All Schedules").range("RoomsAllSchedules")
               If newEI8P.Offset(0, 1).value = newRoom8P.value Then
                newRoom8P.Interior.ColorIndex = 38
               End If
            Next newRoom8P
        End If
    Next newEI8P
    
    ' highlight therapist's rooms on schedule in a different yellow
    For Each initialsCell In Sheets("All Schedules").range("AllSchedTherapistInitialsBox")
        For Each schedCell In Sheets("All Schedules").range(initialsCell.Offset(3, -10), initialsCell.Offset(20, 11))
            If Not IsEmpty(schedCell) Then
                initialsValue = returnInitials(schedCell.value)
                If initialsCell.value = initialsValue Then
                    schedCell.Interior.ColorIndex = 6
                End If
            End If
        Next schedCell
    Next initialsCell
    
End Sub


' clear highlighting and populate schedules; scroll back to top of page
Public Sub createSchedules()
    ' declare variables
    Dim timeCreated As range
    ' clear highlighting
    Sheets("All Schedules").Cells.Interior.ColorIndex = 2
    
    ' set box for displaying time schedule was created
    Set timeCreated = Sheets("All Schedules").range("$AC$14")
    
    ' disable alerts about replacing data in non-blank cells
    Application.DisplayAlerts = False
    
    ' get initials
    Call getInitialsAndNotes
    ' get rooms
    Call getRooms
    ' get 3W and 8P schedules
    Call getSchedules
    ' count therapists
    Call countTherapists
    ' show time created
    Call lastTimeCreated(timeCreated)
    ' apply highlighting
    Call allSchedCondFormat
    ' go back to top of sheet
    ActiveWindow.ScrollRow = 1
    ' turn alerts back on
    Application.DisplayAlerts = True
End Sub

